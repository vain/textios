#!/usr/bin/awk -f

BEGIN {
	FS = "="
	print "'Host';'Service';'Status';'_';'_';'Attempt';'Status_Information'"
}

/^host/ {
	service_description = "HOST"
}

/^\thost_name/ {
	host_name = $2
}

/^\tservice_description/ {
	service_description = $2
}

/^\tcurrent_attempt/ {
	current_attempt = $2
}

/^\tmax_attempts/ {
	max_attempts = $2
}

/^\tcurrent_state/ {
	switch ($2) {
		case 0:
			current_state = "OK"
			break
		case 1:
			current_state = "WARNING"
			break
		case 2:
			current_state = "CRITICAL"
			break
		case 3:
			current_state = "UNKNOWN"
			break
	}
}

/^\tplugin_output/ {
	plugin_output = substr($0, index($0, "=") + 1)
}

/^\tscheduled_downtime_depth/ {
	if ($2 != 0) {
		hosts_downtime[host_name] = 1
		host_name = ""
	}
}

/^\tproblem_has_been_acknowledged=1/ {
	# show_acked is a parameter that's only been set by htmlios.
	if ($2 != 0 && show_acked != 1) {
		host_name = ""
	}
}

/^\t}/ {
	if (host_name != "" && !(host_name in hosts_downtime)) {
		print "'" host_name "';'" service_description "';'" \
		      current_state "';'_';'_';'" \
		      current_attempt "/" max_attempts "';'" plugin_output "'"
	}
}
