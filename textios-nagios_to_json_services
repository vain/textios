#!/usr/bin/awk -f

BEGIN {
	FS = "="
	print "{\"status\":{\"service_status\":["
}

/^service/ {
	in_correct_block = 1
}

/^\thost_name/ {
	host_name = $2
}

/^\tservice_description/ {
	service_name = $2
}

/^\tplugin_output/ {
	status_information = $2
	gsub(/"/, "'", status_information)
}

/^\tstate_type/ {
	if ($2 == 0) {
		state_type = "SOFT"
	}
	else if ($2 == 1) {
		state_type = "HARD"
	}
}

/^\tcurrent_state/ {
	if ($2 == 0) {
		current_state = "OK"
	}
	else if ($2 == 1) {
		current_state = "WARNING"
	}
	else if ($2 == 2) {
		current_state = "CRITICAL"
	}
	else if ($2 == 3) {
		current_state = "UNKNOWN"
	}
}

/^\tscheduled_downtime_depth/ {
	if ($2 != 0) {
		downtime = 1
	}
}

/^\tproblem_has_been_acknowledged/ {
	if ($2 != 0) {
		acked = 1
	}
}

/^\t}/ {
	if (in_correct_block == 1) {
		if (first == 0) {
			first = 1
		}
		else {
			printf ","
		}

		printf "{"\
		       "\"host_name\": \"%s\", \"host_display_name\": \"%s\"" \
		       ", \"service_display_name\": \"%s\"" \
		       ", \"status\": \"%s\"" \
		       ", \"state_type\": \"%s\"" \
		       ", \"in_scheduled_downtime\": %s" \
		       ", \"has_been_acknowledged\": %s" \
		       ", \"status_information\": \"%s\"" \
		       "}\n", host_name, host_name, \
		       service_name, \
		       current_state, \
		       state_type, \
		       (downtime == 1 ? "true" : "false"), \
		       (acked == 1 ? "true" : "false"), \
		       status_information

		downtime = 0
		acked = 0
		in_correct_block = 0
	}
}

END {
	print "]}}"
}
